// Grammar for the historic mode of the Linky meter

// Description of the stream
start: frame+

// Frames
frame: _START_FRAME group+ _END_FRAME_COMPLETE  -> complete_frame
     | _START_FRAME group+ _END_FRAME_TRUNCATED -> truncated_frame
_START_FRAME: "\x02"          // start of text "STX"
_END_FRAME_COMPLETE: "\x03"   // end of text "ETX"
_END_FRAME_TRUNCATED: "\x04"  // end of transmission "EOT"

// Groups
group: "\n" _content "\r"
_content: payload _CONTENT_SEPARATOR CHECKSUM
payload: LABEL PAYLOAD_SEPARATOR DATA
_CONTENT_SEPARATOR: SEPARATOR
PAYLOAD_SEPARATOR: SEPARATOR
SEPARATOR: " "     // space
CHECKSUM: /./
LABEL: "ADCO" | "OPTARIF" | "ISOUSC" | "BASE" | "PTEC" | "IINST" | "IMAX" | "PAPP" | "HHPHC" | "MOTDETAT"
DATA: /[^ ]+/
